apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion 19
        targetSdkVersion 29
        versionCode rootProject.versionCode as int
        versionName rootProject.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable true
            shrinkResources false
            zipAlignEnabled false
            //signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            minifyEnabled false
            debuggable true
            //signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    android.applicationVariants.all {
        variant ->
            def buildTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00"))
            def flavor = variant.productFlavors.collect { it.name }.join('-')

            variant.outputs.all { output ->
                outputFileName = [
                        rootProject.applicationName,
                        "v${variant.versionName}",
                        buildTime,
                        flavor.length() > 0 ? "[${flavor}]" : "",
                        variant.buildType.name
                ].findAll { it.length() > 0 }.join('_') << ".apk"
            }
    }
    flavorDimensions "env"
    productFlavors {

        qa {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"http://tech-qa.zhidaohulian.com/\""
            buildConfigField "String", "PATH", "\"/ptech-gateway/scm/\""
        }

        online {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"https://tech.zhidaohulian.com/\""
            buildConfigField "String", "PATH", "\"/carlife\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // 网络
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okio:okio:1.14.0'

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    def lifecycle_version = "2.2.0"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'


    implementation 'com.liulishuo.filedownloader:library:1.7.7'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha02'

}
